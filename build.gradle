plugins {
    id 'fabric-loom' version '1.1-SNAPSHOT'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name + "-" + project.minecraft_version
version = project.mod_version
group = project.maven_group

loom {
    accessWidenerPath = file("src/main/resources/horsebuff.accesswidener")
}

configurations {
    modIncludeImplementation
    include.extendsFrom modIncludeImplementation
    modImplementation.extendsFrom modIncludeImplementation
}

repositories {
    maven {
        name 'ClothConfig'
        url "https://maven.shedaniel.me/"
    }
    maven {
        name 'ModMenu'
        url "https://maven.terraformersmc.com/"
    }
    maven {
        name 'MixinExtras'
        url 'https://jitpack.io'
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings_version}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"

    // Fabric API
    // modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modIncludeImplementation(fabricApi.module("fabric-api-base", project.fabric_api_version))
    modIncludeImplementation(fabricApi.module("fabric-rendering-data-attachment-v1", project.fabric_api_version))
    modIncludeImplementation(fabricApi.module("fabric-lifecycle-events-v1", project.fabric_api_version))
    modIncludeImplementation(fabricApi.module("fabric-registry-sync-v0", project.fabric_api_version))

    // Cloth Config
    modIncludeImplementation("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    // Cloth devs wont update fabric api, so you cant use it certain versions without excluding it's fabric api
    // but because you excluded its instance of fabric api, you need to re-add the bits it uses
    modIncludeImplementation(fabricApi.module("fabric-screen-api-v1", project.fabric_api_version))
    modIncludeImplementation(fabricApi.module("fabric-resource-loader-v0", project.fabric_api_version))

    // Mod Menu
    modImplementation("com.terraformersmc:modmenu:${project.mod_menu_version}")

    // Mixin Extras
    include "com.github.LlamaLad7:MixinExtras:${project.mixin_extras_version}"
    implementation("com.github.LlamaLad7:MixinExtras:${project.mixin_extras_version}")
    annotationProcessor("com.github.LlamaLad7:MixinExtras:${project.mixin_extras_version}")
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand project.properties
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release.set(17)
}

jar {
    from(rootProject.file("LICENSE")) {
        rename { "${it}_${archivesBaseName}" }
    }
}